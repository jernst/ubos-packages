# From Arch AUR, with modification for UBOS
# https://aur.archlinux.org/cgit/aur.git/plain/PKGBUILD?h=monero

maintainer="http://indiecomputing.com/"
developer='https://getmonero.org/'
url=${developer}
maintainer=http://indiecomputing.com/
pkgname=$(basename $(pwd))
pkgver=0.12.0.0
pkgrel=1
pkgdesc="Monero: the secure, private, untraceable currency"
license=('custom:monero')
arch=('x86_64' 'armv7h' 'aarch64')
depends=('boost-libs' 'unbound' 'miniupnpc' 'libunwind' 'openssl')
makedepends=('git' 'cmake' 'boost' 'gtest')
provides=('monero' 'libmonero-wallet')
backup=('etc/monero/monerod.conf')
install=monero.install
releasepage=("https://aur.archlinux.org/cgit/aur.git/plain/PKGBUILD?h=monero")
source=("https://github.com/monero-project/monero/archive/v${pkgver}.tar.gz"
        "monerod.service"
        "cmake-wallet.patch")

sha512sums=('780465569cdd7cf7f38e5e6dc88f420f411e3768ee0e0421cfb2f1ccb48b2fa93261d8f806f51f7fde44f622395c9b90809fea7bb50ad3470a34b8df80bf882e'
            'bbb023b41c8cdea1f2157de8e83b24f9aaedc63ae6348766afd027b3079190dd1eddfd1f218a87e68a71a99f203a2b65526903041f4c3e386b0154a22607aa78'
            'd9fbc3aff46947a5787d9377c1c2569ee7d78b68afe7b38f11a8dbd1e299930ab864f1351552bc72fa7eb8fb5c39182aea4dfab2f9844ae08cdd78285627edfa')

# NOTE: this is crucial, otherwise stripping breaks the .a archive:
# monero-core (GUI) fails to link against it (it can't find symbols
# that are clearly in the library).
options=(!strip)

_monero="${pkgname}-${pkgver}"
_build=build

prepare() {
    cd "${srcdir}/${_monero}"
    patch -Np1 -i "${srcdir}/cmake-wallet.patch"
}

build() {
    cd "${srcdir}/${_monero}"
    CMAKE_FLAGS+=" -DCMAKE_BUILD_TYPE=Release "
    CMAKE_FLAGS+=" -DCMAKE_INSTALL_PREFIX=/usr "
    CMAKE_FLAGS+=" -DBUILD_TESTS=ON "
    CMAKE_FLAGS+=" -DBUILD_GUI_DEPS=ON "
    #CMAKE_FLAGS+=" -DCMAKE_LINKER=/usr/bin/ld.gold " # #974 ld segfault on ARM, re-implement if needed
    mkdir -p $_build && cd $_build
    cmake $CMAKE_FLAGS ../
    make
}

check() {
    cd "$srcdir/$_monero"
    cd build

    # Temporarily disable some a tests:
    #  * coretests takes too long (~25000s)
    #  * libwallet_api_tests fail (Issue #895)
    #  * unit_tests were run separately above
    CTEST_ARGS+="-E 'coretests|libwallet_api_tests|unit_tests'"
    echo ">>> NOTE: some tests excluded: $CTEST_ARGS"

    make ARGS="$CTEST_ARGS" test
}

package_monero() {

    install -Dm755 "${srcdir}/${_monero}/build/bin/monero-blockchain-export" "${pkgdir}/usr/bin/monero-blockchain-export"
    install -Dm755 "${srcdir}/${_monero}/build/bin/monero-blockchain-import" "${pkgdir}/usr/bin/monero-blockchain-import"
    install -Dm755 "${srcdir}/${_monero}/build/bin/monero-wallet-cli" "${pkgdir}/usr/bin/monero-wallet-cli"
    install -Dm755 "${srcdir}/${_monero}/build/bin/monero-wallet-rpc" "${pkgdir}/usr/bin/monero-wallet-rpc"
    install -Dm755 "${srcdir}/${_monero}/build/bin/monerod" "${pkgdir}/usr/bin/monerod"

    install -Dm644 "${srcdir}/${_monero}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    install -Dm644 "${srcdir}/${_monero}/utils/conf/monerod.conf" "${pkgdir}/etc/monero/monerod.conf"
    # TODO(anonimal): new, working, systemd service file was not merged into monero branch v0.11.1.0 - so we've git-add'd it ourselves
    #install -Dm644 "${srcdir}/${_monero}/utils/systemd/monerod.service" "${pkgdir}/usr/lib/systemd/system/monerod.service"
    install -Dm644 "${srcdir}/monerod.service" "${pkgdir}/usr/lib/systemd/system/monerod.service"

    # License
    install -Dm644 "${srcdir}/${_monero}/LICENSE" -t "${pkgdir}/usr/share/licenses/monero/"
}

package_libmonero-wallet() {

    _stage_dir=stagedir

    cd "${srcdir}/${_monero}/${_build}"

    mkdir -p $_stage_dir
    make DESTDIR=$_stage_dir install

    cd $_stage_dir
    find usr/{include,lib} -type f -exec install -D -m 755 {} ${pkgdir}/{} \;
}
