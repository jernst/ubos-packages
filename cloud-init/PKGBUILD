#
# Upstream maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Patches from: https://www.uplinklabs.net/projects/arch-linux-on-ec2/
# Patches from: Indie Computing Corp.
#

pkgname=cloud-init
pkgver=0.7.6
pkgrel=8
pkgdesc="Provides configuration and customization for cloud instances"
arch=(any)
license=("GPL3")
url=https://launchpad.net/cloud-init

# cloud-init python lib requirements (according to Requires file, with added setuptools):
depends=(systemd
         net-tools
         python2-boto
         python2-cheetah
         python2-configobj
         python2-jsonpatch
         python2-jsonpointer
         python2-oauth
         python2-prettytable
         python2-setuptools
         python2-yaml
         )
# snoonan also has these dependencies:
#         python2-grequests
#         python2-oauth2
#         python2-requests

makedepends=('python2' 'python2-setuptools')
options=(!emptydirs)

source=(https://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz)
sha512sums=('aa2397328afda8e77ed6d642e7642f41a4b4b5bcd3d0e87056aa8c88b56624ec65c57cfc66b0d13ccc235f1840baf1d577316974902a0439cf2f2eb0f8eef36d')
backup=(etc/cloud/cloud.cfg etc/cloud/cloud.cfg.d/05_logging.cfg)

prepare(){
    # change python to python2
    find $pkgname-$pkgver -name \*.py -exec sed -i '1s/python$/&2/' {} +
    sed -i '1s/python$/&2/' $pkgname-$pkgver/tools/read*
    sed -e 's:/etc/systemd:/usr/lib/systemd:g' -e 's:\"/lib\":\"/usr/lib\":g' -i $pkgname-$pkgver/setup.py

    cd $pkgname-$pkgver
    perl -pi -e 's/network-online.target/network.target/g' systemd/*.service
    patch -Np1 -i "${startdir}/fix-logging.patch"
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  python2 ./setup.py install --root=${pkgdir} --init-system systemd

  # Use a cloud.cfg crafted for UBOS
  cp "${startdir}/ubos.cloud.cfg" "${pkgdir}/etc/cloud/cloud.cfg"
}

